#!/usr/local/bin/slash

var files = (find "cases/" -type f)
var successes = 0
var tests = (echo $files | wc -l) as num

var failed_tests = []

loop file in $files {
    printf '\033[94mCheck %s:\033[0m\n' $file
    var content = (cat $file)
    
    var source = (echo $content | sed -n '/^--- SOURCE ---$/,/^--- OUTPUT ---$/p' | sed '1d;$d')
    var output = (echo $content | sed -n '/^--- OUTPUT ---$/,$p' | sed '1d')

    var source_file = (mktemp)
    echo $source > $source_file
    var output_actual = (./metagenc -t c $source_file)
    var output_actual_bytecode = (./metagenc -t bytecode $source_file)

    # Make temporary files
    var output_file = (mktemp)
    var output_actual_file = (mktemp)
    var output_actual_bytecode_file = (mktemp)
    
    echo $output > $output_file
    echo $output_actual > $output_actual_file
    echo $output_actual_bytecode > $output_actual_bytecode_file
    
    var failed = false
    diff -Z $output_actual_file $output_file
    if $? != 0 {
        printf '\033[91mDifferent output!\033[0m\n\n'
        $failed = true
    } 

    diff -Z $output_actual_bytecode_file $output_file
    if $? != 0 {
        printf '\033[91mDifferent bytecode output!\033[0m\n\n'
        $failed = true
    } 
    
    if $failed == false {
        $successes += 1
        printf '\033[92mSucceeded\033[0m\n\n'
    } else {
        $failed_tests += [$file]
    }
    
    # Remove temporary files
    rm $output_file $output_actual_file $output_actual_bytecode_file
}

echo "-- Summary -- "
echo $successes "out of" $tests "tests succeeded"

if $successes != $tests {
    echo "Failed:"
    loop failed_test in $failed_tests {
        echo $failed_test
    }
}

rm out.c a.out
